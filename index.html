<html>
<head>

    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1" />

    <title>~致小熊~</title>

    <link rel="stylesheet" type="text/css" href="css/core.css" />
    <link rel="stylesheet" type="text/css" href="css/animate.css" />
    <link rel="stylesheet" type="text/css" href="css/flavr.min.css" />
    <link rel="stylesheet" type="text/css" href="css/moment.css" />

    <style>
        body {
            padding: 0;
            margin: 0;
            overflow: hidden;
            height: 600px;
        }

        canvas {
            padding: 0;
            margin: 0;
        }

        div.content {
            position: fixed;
            left: 0;
            top: 0;
        }
    </style>
</head>

<body>

    <canvas id="sakura"></canvas>

    <div class="content"></div>

    <!-- sakura shader -->

    <script id="sakura_point_vsh" type="x-shader/x_vertex">

uniform mat4 uProjection;

uniform mat4 uModelview;

uniform vec3 uResolution;

uniform vec3 uOffset;

uniform vec3 uDOF;  //x:focus distance, y:focus radius, z:max radius

uniform vec3 uFade; //x:start distance, y:half distance, z:near fade start

attribute vec3 aPosition;

attribute vec3 aEuler;

attribute vec2 aMisc; //x:size, y:fade

varying vec3 pposition;

varying float psize;

varying float palpha;

varying float pdist;

//varying mat3 rotMat;

varying vec3 normX;

varying vec3 normY;

varying vec3 normZ;

varying vec3 normal;

varying float diffuse;

varying float specular;

varying float rstop;

varying float distancefade;

void main(void) {

    // Projection is based on vertical angle

    vec4 pos = uModelview * vec4(aPosition + uOffset, 1.0);

    gl_Position = uProjection * pos;

    gl_PointSize = aMisc.x * uProjection[1][1] / -pos.z * uResolution.y * 0.5;

    pposition = pos.xyz;

    psize = aMisc.x;

    pdist = length(pos.xyz);

    palpha = smoothstep(0.0, 1.0, (pdist - 0.1) / uFade.z);

    vec3 elrsn = sin(aEuler);

    vec3 elrcs = cos(aEuler);

    mat3 rotx = mat3(

        1.0, 0.0, 0.0,

        0.0, elrcs.x, elrsn.x,

        0.0, -elrsn.x, elrcs.x

    );

    mat3 roty = mat3(

        elrcs.y, 0.0, -elrsn.y,

        0.0, 1.0, 0.0,

        elrsn.y, 0.0, elrcs.y

    );

    mat3 rotz = mat3(

        elrcs.z, elrsn.z, 0.0,

        -elrsn.z, elrcs.z, 0.0,

        0.0, 0.0, 1.0

    );

    mat3 rotmat = rotx * roty * rotz;

    normal = rotmat[2];

    mat3 trrotm = mat3(

        rotmat[0][0], rotmat[1][0], rotmat[2][0],

        rotmat[0][1], rotmat[1][1], rotmat[2][1],

        rotmat[0][2], rotmat[1][2], rotmat[2][2]

    );

    normX = trrotm[0];

    normY = trrotm[1];

    normZ = trrotm[2];

    const vec3 lit = vec3(0.6917144638660746, 0.6917144638660746, -0.20751433915982237);

    float tmpdfs = dot(lit, normal);

    if(tmpdfs < 0.0) {

        normal = -normal;

        tmpdfs = dot(lit, normal);

    }

    diffuse = 0.4 + tmpdfs;

    vec3 eyev = normalize(-pos.xyz);

    if(dot(eyev, normal) > 0.0) {

        vec3 hv = normalize(eyev + lit);

        specular = pow(max(dot(hv, normal), 0.0), 20.0);

    }

    else {

        specular = 0.0;

    }

    rstop = clamp((abs(pdist - uDOF.x) - uDOF.y) / uDOF.z, 0.0, 1.0);

    rstop = pow(rstop, 0.5);

    //-0.69315 = ln(0.5)

    distancefade = min(1.0, exp((uFade.x - pdist) * 0.69315 / uFade.y));

}
    </script>

    <script id="sakura_point_fsh" type="x-shader/x_fragment">

#ifdef GL_ES

//precision mediump float;

precision highp float;

#endif

uniform vec3 uDOF;  //x:focus distance, y:focus radius, z:max radius

uniform vec3 uFade; //x:start distance, y:half distance, z:near fade start

const vec3 fadeCol = vec3(0.08, 0.03, 0.06);

varying vec3 pposition;

varying float psize;

varying float palpha;

varying float pdist;

//varying mat3 rotMat;

varying vec3 normX;

varying vec3 normY;

varying vec3 normZ;

varying vec3 normal;

varying float diffuse;

varying float specular;

varying float rstop;

varying float distancefade;

float ellipse(vec2 p, vec2 o, vec2 r) {

    vec2 lp = (p - o) / r;

    return length(lp) - 1.0;

}

void main(void) {

    vec3 p = vec3(gl_PointCoord - vec2(0.5, 0.5), 0.0) * 2.0;

    vec3 d = vec3(0.0, 0.0, -1.0);

    float nd = normZ.z; //dot(-normZ, d);

    if(abs(nd) < 0.0001) discard;

    float np = dot(normZ, p);

    vec3 tp = p + d * np / nd;

    vec2 coord = vec2(dot(normX, tp), dot(normY, tp));

    //angle = 15 degree

    const float flwrsn = 0.258819045102521;

    const float flwrcs = 0.965925826289068;

    mat2 flwrm = mat2(flwrcs, -flwrsn, flwrsn, flwrcs);

    vec2 flwrp = vec2(abs(coord.x), coord.y) * flwrm;

    float r;

    if(flwrp.x < 0.0) {

        r = ellipse(flwrp, vec2(0.065, 0.024) * 0.5, vec2(0.36, 0.96) * 0.5);

    }

    else {

        r = ellipse(flwrp, vec2(0.065, 0.024) * 0.5, vec2(0.58, 0.96) * 0.5);

    }

    if(r > rstop) discard;

    vec3 col = mix(vec3(1.0, 0.8, 0.75), vec3(1.0, 0.9, 0.87), r);

    float grady = mix(0.0, 1.0, pow(coord.y * 0.5 + 0.5, 0.35));

    col *= vec3(1.0, grady, grady);

    col *= mix(0.8, 1.0, pow(abs(coord.x), 0.3));

    col = col * diffuse + specular;

    col = mix(fadeCol, col, distancefade);

    float alpha = (rstop > 0.001)? (0.5 - r / (rstop * 2.0)) : 1.0;

    alpha = smoothstep(0.0, 1.0, alpha) * palpha;

    gl_FragColor = vec4(col * 0.5, alpha);

}
    </script>

    <!-- effects -->

    <script id="fx_common_vsh" type="x-shader/x_vertex">

uniform vec3 uResolution;

attribute vec2 aPosition;

varying vec2 texCoord;

varying vec2 screenCoord;

void main(void) {

    gl_Position = vec4(aPosition, 0.0, 1.0);

    texCoord = aPosition.xy * 0.5 + vec2(0.5, 0.5);

    screenCoord = aPosition.xy * vec2(uResolution.z, 1.0);

}
    </script>

    <script id="bg_fsh" type="x-shader/x_fragment">

#ifdef GL_ES

//precision mediump float;

precision highp float;

#endif

uniform vec2 uTimes;

varying vec2 texCoord;

varying vec2 screenCoord;

void main(void) {

    vec3 col;

    float c;

    vec2 tmpv = texCoord * vec2(0.8, 1.0) - vec2(0.95, 1.0);

    c = exp(-pow(length(tmpv) * 1.8, 2.0));

    col = mix(vec3(0.02, 0.0, 0.03), vec3(0.96, 0.98, 1.0) * 1.5, c);

    gl_FragColor = vec4(col * 0.5, 1.0);

}
    </script>

    <script id="fx_brightbuf_fsh" type="x-shader/x_fragment">

#ifdef GL_ES

//precision mediump float;

precision highp float;

#endif

uniform sampler2D uSrc;

uniform vec2 uDelta;

varying vec2 texCoord;

varying vec2 screenCoord;

void main(void) {

    vec4 col = texture2D(uSrc, texCoord);

    gl_FragColor = vec4(col.rgb * 2.0 - vec3(0.5), 1.0);

}
    </script>

    <script id="fx_dirblur_r4_fsh" type="x-shader/x_fragment">

#ifdef GL_ES

//precision mediump float;

precision highp float;

#endif

uniform sampler2D uSrc;

uniform vec2 uDelta;

uniform vec4 uBlurDir; //dir(x, y), stride(z, w)

varying vec2 texCoord;

varying vec2 screenCoord;

void main(void) {

    vec4 col = texture2D(uSrc, texCoord);

    col = col + texture2D(uSrc, texCoord + uBlurDir.xy * uDelta);

    col = col + texture2D(uSrc, texCoord - uBlurDir.xy * uDelta);

    col = col + texture2D(uSrc, texCoord + (uBlurDir.xy + uBlurDir.zw) * uDelta);

    col = col + texture2D(uSrc, texCoord - (uBlurDir.xy + uBlurDir.zw) * uDelta);

    gl_FragColor = col / 5.0;

}
    </script>

    <!-- effect fragment shader template -->

    <script id="fx_common_fsh" type="x-shader/x_fragment">

#ifdef GL_ES

//precision mediump float;

precision highp float;

#endif

uniform sampler2D uSrc;

uniform vec2 uDelta;

varying vec2 texCoord;

varying vec2 screenCoord;

void main(void) {

    gl_FragColor = texture2D(uSrc, texCoord);

}
    </script>

    <!-- post processing -->

    <script id="pp_final_vsh" type="x-shader/x_vertex">

uniform vec3 uResolution;

attribute vec2 aPosition;

varying vec2 texCoord;

varying vec2 screenCoord;

void main(void) {

    gl_Position = vec4(aPosition, 0.0, 1.0);

    texCoord = aPosition.xy * 0.5 + vec2(0.5, 0.5);

    screenCoord = aPosition.xy * vec2(uResolution.z, 1.0);

}
    </script>

    <script id="pp_final_fsh" type="x-shader/x_fragment">

#ifdef GL_ES

//precision mediump float;

precision highp float;

#endif

uniform sampler2D uSrc;

uniform sampler2D uBloom;

uniform vec2 uDelta;

varying vec2 texCoord;

varying vec2 screenCoord;

void main(void) {

    vec4 srccol = texture2D(uSrc, texCoord) * 2.0;

    vec4 bloomcol = texture2D(uBloom, texCoord);

    vec4 col;

    col = srccol + bloomcol * (vec4(1.0) + srccol);

    col *= smoothstep(1.0, 0.0, pow(length((texCoord - vec2(0.5)) * 2.0), 1.2) * 0.5);

    col = pow(col, vec4(0.45454545454545)); //(1.0 / 2.2)

    gl_FragColor = vec4(col.rgb, 1.0);

    gl_FragColor.a = 1.0;

}
    </script>

    <script type="text/javascript" src="js/cherry.js"></script>
    <script type="text/javascript">
        var cherry = new Cherry();
        cherry.start();
    </script>

    <!-- jquery -->
    <script src="http://lib.sinaapp.com/js/jquery/1.9.1/jquery-1.9.1.min.js"></script>

    <!-- flavr -->
    <script type="text/javascript" src="js/flavr.min.js"></script>

    <!-- typewriter -->
    <script type="text/javascript" src="js/typewriter.js"></script>

    <script type="text/javascript" src="js/util.js"></script>

    <!-- content -->
    <div id="content" style="display: none">
        <div class="lazyShow">
            <div class="hero-circle">
                <div class="hero-face">
                    <div class="hero-hour"></div>
                    <div class="hero-minute"></div>
                    <div class="hero-second"></div>
                </div>
            </div>

            <div class="bottomContain h80">
                <span style="position: absolute; bottom: 0; left: 85px;">小熊，这是我们一起走过的第<span class="timeString"></span></span>
            </div>
            <hr class="clear">
        </div>

        <div class="typewriter">
            <span>小熊，</span><br />
            <span>和你在一起的日子里，</span><br />
            <span>我体验到了幸福的味道<img src="image/expression/hx.png" /></span><br />
            <span></span>
            <br />

            <span>谢谢你对我的包容，</span><br />
            <span>不介意我的懒，不介意我的笨，不介意我的健忘......</span><br />
            <span></span>
            <br />

            <span>我会好好珍惜......</span><br />
            <span>用心地珍惜你、爱护你、陪你一起慢慢变老......<img src="image/expression/qq.png" /></span><br />
            <span></span>
            <br />

            <span>小熊，背后的这片樱花送给你~</span><br />
            <span>愿你永远美丽、善良~~</span><br />
            <span></span>
            <br />

            <span>I Love u !</span>
        </div>
    </div>

    <div style="display: none">
        <div class="hideContent" data-width="490" data-height="356" data-date="20150909">
            <span>小熊，</span><br />
            <span>有时，你是降临到地球的天使，</span><br />
            <span>阳光善良，热爱生活~~</span><br />
            <span></span>
            <br />

            <span>有时，你像是从地狱来的恶魔……残害生灵，</span><br />
            <span>连一只小小的蚊子都不放过，要折磨致死~~</span><br />
            <span></span>
            <br />

            <span>I Love u !</span><br />
        </div>
        <div class="hideContent" data-width="490" data-height="300">
            <span>小熊，</span><br />
            <span>虽然你总爱大吼大叫，</span><br />
            <span>但我就是喜欢，喜欢真实的你~~<img src="image/expression/ka.png" /></span><br />
            <span></span>
            <br />

            <span>I Love u !</span><br />
        </div>
        <div class="hideContent" data-width="490" data-height="300">
            <span>小熊，</span><br />
            <span>我会每天都抽时间陪你，</span><br />
            <span>陪你一起走到时间的尽头......<img src="image/expression/fd.png" /></span><br />
            <span></span>
            <br />

            <span>I Love u !</span><br />
        </div>
        <div class="hideContent" data-width="490" data-height="300">
            <span>小熊，</span><br />
            <span>我要带你爬山、带你看海、带你遨游云端，</span><br />
            <span>带你一起仔细口味这世界的美丽，看美丽的小熊~~~<img src="image/expression/cy.png" /></span><br />
            <span></span>
            <br />

            <span>I Love u !</span><br />
        </div>
        <div class="hideContent" data-width="490" data-height="300">
            <span>小熊，</span><br />
            <span>未来的日子里，我们要永不止步，</span><br />
            <span>不断地学习新知识，永远保持一颗乐观的心~~~<img src="image/expression/ty.png" /></span><br />
            <span></span>
            <br />

            <span>I Love u !</span><br />
        </div>
    </div>

    <script type="text/javascript">

        var control = function () {
            var me = this;
            var startDateTime = new Date(2014, 6, 11, 0, 0, 0, 0);

            me.initDialog = function () {
                var html = $('#content').html();
                var picker = new ContentPicker($('.hideContent'));

                var flavr = new $.flavr({
                    opacity: 0.1,
                    closeEsc: true,
                    content: html,
                    buttons: {
                        success: {
                            text: '还有吗',
                            style: 'success',
                            action: function () {
                                var target = $('.typewriter');
                                var interval = 1500;

                                var callback = function () {
                                    var dom = $(picker.getNext());

                                    var content = dom.html();
                                    var width = dom.attr('data-width');
                                    var height = dom.attr('data-height');

                                    target.html(content);
                                    flavr.resize(width, height);
                                    target.fadeIn(1500);
                                }

                                target.fadeOut(interval);
                                var timeout = setTimeout(callback, interval);

                                return false;
                            }
                        },
                        close: { text: '关闭', style: 'default' }
                    },
                    onShow: function () {
                        this.width(490);
                        var buttons = $('.flavr-button');
                        buttons.hide();

                        $('.typewriter:last').typewriter(
                            8000,
                            function () { buttons.fadeIn(1500); },
                            function () { flavr.resize(490, 450); }
                            );

                        $('.lazyShow').css('opacity', 0);
                        setTimeout(function () { $('.lazyShow').animate({ opacity: "1" }, 3000); }, 2000);
                    }
                });
            };
            me.updateClock = function () {
                var now = new Date(),
                    second = now.getSeconds() * 6,
                    minute = now.getMinutes() * 6 + second / 60,
                    hour = ((now.getHours() % 12) / 12) * 360 + 90 + minute / 12;

                $('.hero-hour').css("transform", "rotate(" + hour + "deg)");
                $('.hero-minute').css("transform", "rotate(" + minute + "deg)");
                $('.hero-second').css("transform", "rotate(" + second + "deg)");
            };
            me.updateTimeString = function () {
                var timeSpan = calcTimeSpan(startDateTime, new Date());
                $('.timeString').text(timeSpan.days + "天" + timeSpan.hours + "小时" + timeSpan.minutes + "分" + timeSpan.seconds + "秒");
            };
            me.timedUpdate = function () {
                me.updateClock();
                me.updateTimeString();
                setTimeout(me.timedUpdate, 1000);
            };

            return me;
        }();

        control.initDialog();
        control.timedUpdate();

        $('canvas').click(function () {
            control.initDialog();
        });
    </script>
</body>
</html>